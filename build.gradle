plugins {
  id 'eclipse'
  id "org.sonarqube" version "3.3"
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
}

repositories {
  mavenCentral()
}

version = "${version != 'unspecified' ? version : '1.0.0'}"
group = 'io.chocorean.authmod.core'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "Chocorean_authmod-core"
    property "sonar.organization", "chocorean-sc"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

tasks.named('sonarqube').configure {
  dependsOn test
}

dependencies {
  compile group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
  compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
  testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')
  testImplementation('org.junit.jupiter:junit-jupiter-params:5.7.2')
  testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.2')
}

jar {
  manifest {
    attributes([
      "Specification-Title"     : "authmod-core",
      "Specification-Vendor"    : "Chocorean",
      "Specification-Version"   : "1",
      "Implementation-Title"    : project.name,
      "Implementation-Version"  : project.version,
      "Implementation-Vendor"   : "Chocorean",
      "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

publishing {
  publications {
    gpr(MavenPublication) {
      artifactId 'core'
      from(components.java)
    }
  }
}


test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}
